
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

mainClassName = 'com.tramchester.App'

def candidate = System.getenv("CIRCLE_BUILD_NUM") ?: "0"
// override build number if RELEASE_NUMBER set, needed because of way circle ci workflows operate
def buildNumber = System.getenv("RELEASE_NUMBER") ?: candidate
def environment = System.getenv("ENV") ?: "Dev"
def isWindows = System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')

// this is used in the generated dist zip filename
version = 1.0

project.ext {
    dropwizardVersion = '2.0.16'
}

repositories {
    maven {
        url "https://repo.osgeo.org/repository/release/"
    }
    mavenCentral()
    maven {
        url "https://raw.github.com/neo4j-contrib/m2/master/releases"
    }
}

// useful reminder ./gradlew -q dependencyInsight --dependency X.Y:Z
dependencies {
    implementation('io.dropwizard:dropwizard-core:' + dropwizardVersion)
    // slf4j in governator was causing race condition with dropwizard start up code on initial get of logger
    implementation('com.netflix.governator:governator:1.17.11') { exclude group:'org.slf4j', module:'slf4j-api'}
    implementation 'com.google.inject:guice:5.0.1'
    implementation('org.neo4j:neo4j:4.2.4') { exclude group:'org.slf4j', module:'slf4j-nop' }
    implementation 'com.github.cliftonlabs:json-simple:3.1.1',
            'commons-io:commons-io:2.8.0',
            'org.apache.httpcomponents:httpclient:4.5.13',
            'org.apache.commons:commons-collections4:4.4',
            'com.github.ben-manes.caffeine:caffeine:2.8.1'
    implementation('com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.12.2')
    implementation('com.fasterxml.jackson.module:jackson-module-afterburner:2.12.2')
    implementation('com.smoketurner:dropwizard-swagger:2.0.12-1')

    // aws sdk
    implementation('software.amazon.awssdk:cloudwatch:2.16.52')
    implementation('software.amazon.awssdk:s3:2.16.37')
    implementation('software.amazon.awssdk:cloudformation:2.16.37')
    // aws cdk
    implementation 'software.amazon.awscdk:core:1.97.0',
            'software.amazon.awscdk:s3:1.97.0',
            'software.amazon.awscdk:ec2:1.97.0',
            'software.amazon.awscdk:elasticloadbalancingv2:1.97.0',
            'software.amazon.awscdk:autoscaling:1.97.0',
            'software.amazon.awscdk:cdk-cx-api:1.97.0'

    implementation('org.geotools:gt-main:25.0')
    implementation('org.geotools:gt-epsg-hsql:25.0')

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1',
            'org.assertj:assertj-core:3.19.0',
            'io.dropwizard:dropwizard-testing:' + dropwizardVersion,
            'org.easymock:easymock:4.2',
            'org.seleniumhq.selenium:selenium-java:3.141.59',
            'io.appium:java-client:7.5.1',
            'org.hamcrest:hamcrest:2.2'

}

sourceSets {
    main {
        java {
            srcDirs = ['main/src']
        }
    }
    test {
        java {
            srcDirs = ['main/test']
        }
        resources.srcDir file('main/test/resources')
    }
}

processResources {
    exclude('**/app/') // via webpack
}

task cfnassist {
    doLast {
        ant.taskdef(name: 'cfnassist', classname: 'tw.com.ant.CfnAssistAntTask') {
            classpath {
                fileset(dir: 'lib/cfnassist-1.1.37/lib/', includes: 'cfnassist-all-1.1.37.jar')
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.deprecation = true
    options.compilerArgs.addAll(['-Xlint'])
}

task stage(dependsOn: ['clean', 'installApp'])

task cleanGraph() {
    doLast {
        delete 'databases/integrationBusTest' ,
                'databases/integrationTramTest',
                'databases/integrationTrainTest',
                'databases/integrationNeighboursTest',
                'databases/tramchesterAcceptance.db',
                'tramchester.db', 'buses.db', 'trains.db'
    }
}

clean {
    dependsOn 'cleanGraph'
    delete 'src/main/resources/app/dist/'
    delete 'data/tram/data.zip', 'data/bus/data.zip'
    delete fileTree('data/tram') {
        include '*.txt'
        include '*.zip'
    }
    delete fileTree('data/bus') {
        include '*.txt'
        include '*.zip'
    }
    delete fileTree('data/neighbours') {
        include '*.txt'
    }
    delete fileTree('data/train') {
        include '*.txt'
        include '*.zip'
    }
    delete fileTree('data/naptan') {
        include '*.csv'
        include '*.zip'
    }
    delete fileTree('data/codepo_gb') {
        include '**/*.csv'
    }
    delete('data/codepo_gb')
}

run {
    dependsOn 'webpack'
    args 'server', 'config/local.yml'
}

test {
    useJUnitPlatform()
}

task pullData(type:JavaExec, dependsOn:classes) {
    main = 'com.tramchester.dataimport.FetchDataFromUrlAndUnzip'
    classpath = sourceSets.main.runtimeClasspath
    args 'http://odata.tfgm.com/opendata/downloads/TfGMgtfsnew.zip', 'data', 'tramData-1.0.zip'
}

task cdkApp(type:JavaExec, dependsOn:classes) {
    main = 'com.tramchester.deployment.CdkApp'
    classpath = sourceSets.main.runtimeClasspath
}

task allowhost(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment) {
            WhiteList(port: '443', tag: 'web')
        }
    }
}

task blockhost(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment) {
            BlackList(port: '443', tag: 'web')
        }
    }
}

task buses(type:JavaExec, dependsOn: 'webpack') {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = "3g"
    args 'server', 'config/buses.yml'
}

task trains(type:JavaExec, dependsOn: 'webpack') {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = "3g"
    args 'server', 'config/trains.yml'
}

task all(type:JavaExec, dependsOn: 'webpack') {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = "3g"
    args 'server', 'config/all.yml'
}

task busTest(type: Test) {
    useJUnitPlatform {
        includeTags 'BusTest'
    }
}

task unit(type: Test, dependsOn: compileJava) {
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.unit.*"
        //excludeTestsMatching "com.tramchester.unit.graph.calculation.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started",  "standardError"
    }
}

task liveMessages(type: Test, dependsOn: compileJava) {
    useJUnitPlatform {
        includeTags "liveDataMessages"
    }
    filter {
        includeTestsMatching "com.tramchester.integration.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError"
    }
}

task integration(type: Test, dependsOn: compileJava) {
    minHeapSize = "1000m"
    maxHeapSize = "1750m"
    useJUnitPlatform {
        //excludeTags "liveDataMessages" // hopefully temporary exclusion
        //excludeTags "liveData" // live data is down currently
        //excludeTags "dataExpiry" // ONLY exclude when impending data expiry causes issues
    }
    filter {
        includeTestsMatching "com.tramchester.integration.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError" //, "standardOut"
    }
}

task apiTests(type: Test, dependsOn: compileJava) {
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.integration.resources.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError" //, "standardOut"
    }
}

// allow re-run of the task to aid in performance tuning
allprojects {
    tasks.matching { task -> task.name == "allStationsTest" }.all {
        outputs.upToDateWhen { false }
    }
}

task allStationsTest(type: Test, dependsOn: compileJava) {
    minHeapSize = "1000m"
    maxHeapSize = "1750m"
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.integration.graph.RouteCalculatorTestAllJourneys"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events("passed", "skipped", "failed")
    }
}

task buildGraphTest(type: Test, dependsOn: compileJava) {
    minHeapSize = "1000m"
    maxHeapSize = "1750m"
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.integration.graph.GraphBuildAndStartTest"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started" //, "standardError", "standardOut"
    }
}

task appium(type: Test) {
    dependsOn 'allowhost'
    dependsOn 'compileJava'
    dependsOn 'webpack'
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.acceptance.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError" , "standardOut"
    }
    finalizedBy {
        blockhost
    }
}

task localAppium(type: Test, dependsOn: ['webpack', 'compileJava']) {
    minHeapSize = "1000m"
    maxHeapSize = "2500m"
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.acceptance.*"
    }
    testLogging {
        events "passed", "skipped", "failed", "started", "standardError" , "standardOut"
    }
}

task uploadData(dependsOn: ['cfnassist', 'pullData']) {
    doLast {
        ant.cfnassist(buildNumber: buildNumber, bucketname: 'tramchester2dist') {
            Artifact(name: 'data', value: 'data/tramData-1.0.zip')
            S3Create()
        }
    }
}

task diagrams(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist() {
            Diagrams(target: '.')
        }
    }
}

task npmInstall(type: Exec) {
    inputs.file("package.json")
    inputs.file("package-lock.json")
    outputs.dir("node_modules")

    if (isWindows) {
        commandLine "npm.cmd", "install"
    }
    else {
        commandLine "npm", "install"
    }
}

jar {
    dependsOn 'webpack'
    into 'app', {
        from 'build/resources/main/app'
    }
}

task webpack(type: Exec, dependsOn: npmInstall) {
    inputs.file("package.json")
    inputs.file("package-lock.json")
    inputs.file("webpack.config.js")
    inputs.files(fileTree("src/main/resources/app"))

    if (isWindows) {
        commandLine "./node_modules/.bin/webpack.cmd"
    } else {
        commandLine "./node_modules/.bin/webpack"
    }
}

distZip {
    into('config') {
        from 'config'
    }
}

task uploadApp(dependsOn: ['cfnassist', 'distZip', 'uploadSupportFiles'])  {
    doLast {
        ant.cfnassist(buildNumber: buildNumber, bucketname: 'tramchester2dist') {
            Artifact(name: 'package', value: 'build/distributions/tramchester-1.0.zip')
            S3Create()
        }
    }
}

task uploadSupportFiles(dependsOn: 'cfnassist') {
    doLast {
        ant.cfnassist(buildNumber: buildNumber, bucketname: 'tramchester2dist') {
            Artifact(name: 'install', value: 'deploy/setupTramWebServerAWSLinux.sh')
            Artifact(name: 'cloudinit', value: 'deploy/cloudInitAWSLinux.txt')
            S3Create()
        }
    }
}

task infra(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, sns: 'true', capabilityIAM: 'true') {
            Templates(target: 'deploy/infra/'+environment )
        }
    }
}

task upload(dependsOn: ['uploadData', 'uploadApp'])

task deploy(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, buildNumber: buildNumber, sns: 'true') {
            Templates(target: 'deploy/servers.json')
        }
    }
}

task deployCDK(type: Exec, dependsOn: [npmInstall, classes]) {
    commandLine "./node_modules/.bin/cdk", "deploy"
}

task updateLB(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, buildNumber: buildNumber, sns: 'true') {
            ELBUpdate(typeTag: 'web')
        }
    }
}

task tidy(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment) {
            TidyStacks(target: 'deploy/servers.json', typeTag: 'web')
        }
    }
}

task allowHostDev(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'Dev') {
            WhiteList(port: '443', tag: 'web')
        }
    }
}

task blockHostDev(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'Dev') {
            BlackList(port: '443', tag: 'web')
        }
    }
}

task allowHostUAT(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'UAT') {
            WhiteList(port: '443', tag: 'web')
        }
    }
}

task denyHostUAT(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'UAT') {
            BlackList(port: '443', tag: 'web')
        }
    }
}






