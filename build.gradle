
plugins {
    id 'com.mgd.core.gradle.s3' version '1.1.4'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

import com.mgd.core.gradle.*

mainClassName = 'com.tramchester.App'

def candidate = System.getenv("CIRCLE_BUILD_NUM") ?: "0"
// override build number if RELEASE_NUMBER set, needed because of way circle ci workflows operate
def buildNumber = System.getenv("RELEASE_NUMBER") ?: candidate
def environment = System.getenv("ENV") ?: "Dev"
def isWindows = System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')
def s3prefix = 'dist/' + buildNumber

s3 {
    bucket = 'tramchester2dist'
}

// this is used in the generated dist zip filename
version = 1.0

project.ext {
    dropwizardVersion = '2.0.26'
}

repositories {
    maven {
        url "https://repo.osgeo.org/repository/release/"
    }
    mavenCentral()
    maven {
        url "https://raw.github.com/neo4j-contrib/m2/master/releases"
    }
}

// useful reminder ./gradlew -q dependencyInsight --dependency X.Y:Z
dependencies {
    implementation('io.dropwizard:dropwizard-core:' + dropwizardVersion)
    // slf4j in governator was causing race condition with dropwizard start up code on initial get of logger
    implementation('com.netflix.governator:governator:1.17.12') { exclude group:'org.slf4j', module:'slf4j-api'}
    implementation 'com.google.inject:guice:5.0.1'
    implementation('org.neo4j:neo4j:4.3.9') { exclude group:'org.slf4j', module:'slf4j-nop' }
    implementation 'com.github.cliftonlabs:json-simple:4.0.0',
            'commons-io:commons-io:2.11.0',
            'org.apache.httpcomponents:httpclient:4.5.13',
            'org.apache.commons:commons-collections4:4.4',
            'com.github.ben-manes.caffeine:caffeine:3.0.5'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.0',
            'com.fasterxml.jackson.core:jackson-databind:2.13.0',
            'com.fasterxml.jackson.module:jackson-module-afterburner:2.13.0',
            'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.13.0',
            'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.0'
    implementation('com.smoketurner:dropwizard-swagger:2.0.12-1')

    // aws sdk
    implementation('software.amazon.awssdk:s3:2.17.99')
    implementation('software.amazon.awssdk:cloudwatch:2.17.99')
    implementation('software.amazon.awssdk:cloudformation:2.17.99')
    // aws cdk
    implementation 'software.amazon.awscdk:core:1.143.0',
            'software.amazon.awscdk:s3:1.134.0',
            'software.amazon.awscdk:ec2:1.134.0',
            'software.amazon.awscdk:elasticloadbalancingv2:1.134.0',
            'software.amazon.awscdk:autoscaling:1.134.0',
            'software.amazon.awscdk:cdk-cx-api:2.1.0'

    implementation('org.geotools:gt-main:26.1')
    implementation('org.geotools:gt-epsg-hsql:26.1')

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2',
            'org.junit.platform:junit-platform-launcher:1.8.2',
            'org.assertj:assertj-core:3.21.0',
            'io.dropwizard:dropwizard-testing:' + dropwizardVersion,
            'org.easymock:easymock:4.3',
            'org.seleniumhq.selenium:selenium-java:3.141.59',
            'io.appium:java-client:7.6.0',
            'org.hamcrest:hamcrest:2.2'

}

sourceSets {
    main {
        java {
            srcDirs = ['main/src']
        }
    }
    test {
        java {
            srcDirs = ['main/test']
        }
        resources.srcDir file('main/test/resources')
    }
}

processResources {
    exclude('**/app/') // via webpack
}

task cfnassist {
    doLast {
        ant.taskdef(name: 'cfnassist', classname: 'tw.com.ant.CfnAssistAntTask') {
            classpath {
                fileset(dir: 'lib/cfnassist-1.1.37/lib/', includes: 'cfnassist-all-1.1.37.jar')
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.deprecation = true
    options.compilerArgs.addAll(['-Xlint'])
}

task stage(dependsOn: ['clean', 'installApp'])

task cleanGraph() {
    doLast {
        delete 'databases/integrationBusTest' ,
                'databases/integrationTramTest',
                'databases/integrationTrainTest',
                'databases/integrationNeighboursTest',
                'databases/neighboursTest',
                'databases/integrationTramTrainGMTest',
                'databases/tramchesterAcceptance.db',
                'databases/all.db',
                'tramchester.db', 'buses.db', 'trains.db', 'all.db', 'rail.db', 'gm_rail.db'
    }
}

clean {
    dependsOn 'cleanGraph'
    delete 'src/main/resources/app/dist/'
    delete 'data/tram/data.zip', 'data/bus/data.zip'
    delete fileTree('data/tram') {
        include '*.txt'
        include '*.zip'
        include '*.csv'
    }
    delete fileTree('data/bus') {
        include '*.txt'
        include '*.zip'
        include '*.csv'
    }
    delete fileTree('data/train') {
        include '*.txt'
        include '*.zip'
        include '*.csv'
    }
    delete fileTree('data/naptan') {
        include '*.csv'
        include '*.zip'
    }
    delete fileTree('data/postcodes') {
        include '**/*.csv'
    }
    delete('data/postcodes')
    delete('testData/cache')
}

run {
    // see deploy/setupTramWebServerAWSLinux.sh
    maxHeapSize = '700m'
    dependsOn 'webpack'
    args 'server', 'config/local.yml'
}

test {
    useJUnitPlatform()
}

task cdkApp(type:JavaExec, dependsOn:classes) {
    mainClass = 'com.tramchester.deployment.CdkApp'
    classpath = sourceSets.main.runtimeClasspath
}

task buses(type:JavaExec, dependsOn: 'webpack') {
    mainClass = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = "3g"
    args 'server', 'config/buses.yml'
}

task frequency(type:JavaExec, dependsOn: 'webpack') {
    mainClass = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = "3g"
    args 'server', 'config/frequency.yml'
}

task rail(type:JavaExec, dependsOn: 'webpack') {
    mainClass = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = "3g"
    args 'server', 'config/rail.yml'
}

task gm(type:JavaExec, dependsOn: 'webpack') {
    mainClass = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = "3g"
    args 'server', 'config/gm.yml'
}

task all(type:JavaExec, dependsOn: 'webpack') {
    mainClass = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = "3g"
    args 'server', 'config/all.yml'
}

task busTest(type: Test) {
    useJUnitPlatform {
        includeTags 'BusTest'
    }
}

task unit(type: Test, dependsOn: compileJava) {
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.unit.*"
        //excludeTestsMatching "com.tramchester.unit.graph.calculation.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
    }
}

task liveMessages(type: Test, dependsOn: compileJava) {
    useJUnitPlatform {
        includeTags "liveDataMessages"
    }
    filter {
        includeTestsMatching "com.tramchester.integration.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError"
    }
}

task integration(type: Test, dependsOn: compileJava) {
    minHeapSize = "1000m"
    maxHeapSize = "1750m"
    useJUnitPlatform {
        //excludeTags "liveDataMessages" // these go away periodically...
        //excludeTags "liveData" // also goes away on occasion
        //excludeTags "dataExpiry" // can be useful to exclude when big changes to timetable are upcoming
        excludeTags "postcode" // not needed for tram, but used by buses
        excludeTags "BusTest"
        excludeTags "TrainTest"
    }
    filter {
        includeTestsMatching "com.tramchester.integration.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError" //, "standardOut"
    }
}

task dataUpdated(type: Test, dependsOn: compileJava) {
    minHeapSize = "1000m"
    maxHeapSize = "1750m"
    useJUnitPlatform {
        includeTags "DataUpdateTest"
    }
    filter {
        includeTestsMatching "com.tramchester.integration.*"
    }
    testLogging {
        exceptionFormat "full"
        events "failed"
    }
}

task s3Test(type: Test, dependsOn: compileJava) {
    useJUnitPlatform {
        includeTags "S3Test"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError" //, "standardOut"
    }
}

task integrationBus(type: Test, dependsOn: compileJava) {
    minHeapSize = "1000m"
    maxHeapSize = "3000m"
    useJUnitPlatform {
        includeTags "BusTest"
        includeTags "postcode"
    }
    filter {
        includeTestsMatching "com.tramchester.integration.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError" //, "standardOut"
    }
}

task integrationRail(type: Test, dependsOn: compileJava) {
    minHeapSize = "1000m"
    maxHeapSize = "3000m"
    useJUnitPlatform {
        includeTags "TrainTest"
    }
    filter {
        includeTestsMatching "com.tramchester.integration.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError" //, "standardOut"
    }
}

task apiTests(type: Test, dependsOn: compileJava) {
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.integration.resources.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError" //, "standardOut"
    }
}

task graphTests(type: Test, dependsOn: compileJava) {
    minHeapSize = "1000m"
    maxHeapSize = "1750m"
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.integration.graph.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError" //, "standardOut"
    }
}

// allow re-run of the task to aid in performance tuning
allprojects {
    tasks.matching { task -> task.name == "allStationsTest" }.all {
        outputs.upToDateWhen { false }
    }
}

task allStationsTest(type: Test, dependsOn: compileJava) {
    minHeapSize = "1000m"
    maxHeapSize = "1750m"
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.integration.graph.RouteCalculatorTestAllJourneys"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events("passed", "skipped", "failed")
    }
}

task buildGraphTest(type: Test, dependsOn: compileJava) {
    minHeapSize = "1000m"
    maxHeapSize = "1750m"
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.integration.graph.GraphBuildAndStartTest"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started" //, "standardError", "standardOut"
    }
}

task appium(type: Test) {
    dependsOn 'allowhost'
    dependsOn 'compileJava'
    dependsOn 'webpack'
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.acceptance.*"
    }
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed", "started", "standardError" , "standardOut"
    }
    finalizedBy {
        blockhost
    }
}

task localAppium(type: Test, dependsOn: ['webpack', 'compileJava']) {
    minHeapSize = "1000m"
    maxHeapSize = "2500m"
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.tramchester.acceptance.*"
    }
    testLogging {
        events "passed", "skipped", "failed", "started", "standardError" , "standardOut"
    }
}

task diagrams(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist() {
            Diagrams(target: '.')
        }
    }
}

task npmInstall(type: Exec) {
    inputs.file("package.json")
    inputs.file("package-lock.json")
    outputs.dir("node_modules")

    if (isWindows) {
        commandLine "npm.cmd", "install"
    }
    else {
        commandLine "npm", "install"
    }
}

jar {
    dependsOn 'webpack'
    into 'app', {
        from 'build/resources/main/app'
    }
    exclude('app/*')
}

task webpack(type: Exec, dependsOn: npmInstall) {
    inputs.file("package.json")
    inputs.file("package-lock.json")
    inputs.file("webpack.config.js")
    inputs.files(fileTree("src/main/resources/app"))

    if (isWindows) {
        commandLine "./node_modules/.bin/webpack.cmd"
    } else {
        commandLine "./node_modules/.bin/webpack"
    }
}

distributions {
    main {
        contents {
            into ('config') {
                from 'config'
            }
            into ('bin') {
                from 'deploy/start.sh'
                from 'deploy/nessus_agent_install.sh'
            }
        }
    }
}

task pullData(type:JavaExec, dependsOn:classes) {
    mainClass = 'com.tramchester.deployment.cli.FetchDataCLI'
    classpath = sourceSets.main.runtimeClasspath
    args 'config/local.yml'
}

task buildDatabase(type:JavaExec, dependsOn:classes) {
    mainClass = 'com.tramchester.deployment.cli.BuildGraphCLI'
    classpath = sourceSets.main.runtimeClasspath
    args 'config/local.yml'
}

task zipDB(type:Zip, dependsOn: 'buildDatabase') {
    from 'tramchester.db'
    archiveFileName = 'tramchester.db.zip'
}

task uploadApp(type: S3Upload, dependsOn: ['distZip', 'uploadDeployScript', 'uploadCloudInit'])  {
    key = s3prefix + '/tramchester-1.0.zip'
    file = 'build/distributions/tramchester-1.0.zip'
    overwrite = true
}

task uploadData(type: S3Upload, dependsOn: ['zipDB','uploadTFGMData']) {
    key = s3prefix + '/tramchester.db.zip'
    file = 'build/distributions/tramchester.db.zip'
    overwrite = true
}

task uploadTFGMData(type: S3Upload, dependsOn: ['pullData']) {
    key = s3prefix + '/tfgm_data.zip'
    file = 'data/tram/tfgm_data.zip'
    overwrite = true
}

task uploadDeployScript(type: S3Upload) {
    key = s3prefix + '/setupTramWebServerAWSLinux.sh'
    file = 'deploy/setupTramWebServerAWSLinux.sh'
    overwrite = true
}

task uploadCloudInit(type: S3Upload) {
    key = s3prefix + '/cloudInitAWSLinux.txt'
    file = 'deploy/cloudInitAWSLinux.txt'
    overwrite = true
}

task infra(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, sns: 'true', capabilityIAM: 'true') {
            Templates(target: 'deploy/infra/'+environment )
        }
    }
}

task upload(dependsOn: ['uploadData', 'uploadApp'])

task deploy(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, buildNumber: buildNumber, sns: 'true') {
            Templates(target: 'deploy/servers.json')
        }
    }
}

task deployCDK(type: Exec, dependsOn: [npmInstall, classes]) {
    commandLine "./node_modules/.bin/cdk", "deploy"
}

task updateLB(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, buildNumber: buildNumber, sns: 'true') {
            ELBUpdate(typeTag: 'web')
        }
    }
}

task tidy(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment) {
            TidyStacks(target: 'deploy/servers.json', typeTag: 'web')
        }
    }
}

task allowHostDev(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'Dev') {
            WhiteList(port: '443', tag: 'web')
        }
    }
}

task blockHostDev(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'Dev') {
            BlackList(port: '443', tag: 'web')
        }
    }
}

task allowHostUAT(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'UAT') {
            WhiteList(port: '443', tag: 'web')
        }
    }
}

task denyHostUAT(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'UAT') {
            BlackList(port: '443', tag: 'web')
        }
    }
}

task allowhost(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment) {
            WhiteList(port: '443', tag: 'web')
        }
    }
}

task blockhost(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment) {
            BlackList(port: '443', tag: 'web')
        }
    }
}






