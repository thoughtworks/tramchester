buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:2.1.0'
    }
}
 
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

mainClassName = 'com.tramchester.App'

def candidateNumber = System.getenv("TRAVIS_BUILD_NUMBER") ?: "0"
def buildNumber = System.getenv("CIRCLE_BUILD_NUM") ?: candidateNumber
def environment = System.getenv("ENV") ?: "Dev"

// this is used in the generated dist zip filename
version = 1.0

project.ext {
    dropwizardVersion = '1.1.3'
}

repositories {
    maven {
        url "http://m2.neo4j.org/content/repositories/releases"
    }
    maven {
        url "http://download.osgeo.org/webdav/geotools/"
    }
    maven {
        url "https://raw.github.com/neo4j-contrib/m2/master/releases"
    }
    maven {
        url "http://maven.geo-solutions.it"
    }
    mavenCentral()
}

dependencies {
    compile('io.dropwizard:dropwizard-core:' + dropwizardVersion) { exclude group: 'asm', module: 'asm' }
    compile('io.dropwizard:dropwizard-assets:' + dropwizardVersion) { exclude group: 'asm', module: 'asm' }
    compile 'org.picocontainer:picocontainer:2.14.3',
            'org.neo4j:neo4j:3.1.1',
            'org.neo4j:neo4j-spatial:0.24-neo4j-3.1.1',
            'joda-time:joda-time:2.4',
            'com.googlecode.jcsv:jcsv:1.4.0',
            'net.lingala.zip4j:zip4j:1.2.3',
            'commons-io:commons-io:2.4',
            'org.apache.httpcomponents:httpclient:4.5.1',
            'org.apache.commons:commons-collections4:4.1',
            'com.javadocmd:simplelatlng:1.3.1',
            'com.amazonaws:aws-java-sdk-cloudwatch:1.11.177',
            'com.smoketurner:dropwizard-swagger:1.1.1-1'

    testCompile 'junit:junit:4.11',
            'org.assertj:assertj-core:1.2.0',
            'io.dropwizard:dropwizard-testing:' + dropwizardVersion,
            'org.easymock:easymock:3.4',
            'org.seleniumhq.selenium:selenium-java:3.11.0',
            'io.appium:java-client:4.1.2'
}

sourceSets {
    test {
        java {
            srcDirs = ['src/test']
        }
    }
}

task cfnassist {
    doLast {
        ant.taskdef(name: 'cfnassist', classname: 'tw.com.ant.CfnAssistAntTask') {
            classpath {
                fileset(dir: 'lib/cfnassist-1.1.18/lib/', includes: 'cfnassist-all-1.1.18.jar')
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.deprecation = true
    options.compilerArgs.addAll(['-Xlint'])
}


task stage(dependsOn: ['clean', 'installApp'])

clean {
    delete 'int_test_tramchester.db' , 'data/tram/data.zip', 'data/bus/data.zip', 'int_test_bus_tramchester.db',
            'perf_test_tramchester.db'
    delete fileTree('data/tram') {
        include '*.txt'
    }
    delete fileTree('data/tram/gtdf-out') {
        include '*.txt'
    }
    delete 'data/tram/gtdf-out'
    delete fileTree('data/bus') {
        include '*.txt'
    }
    delete fileTree('data/all') {
        include '*.txt'
    }
}

run {
    args 'server', 'config/local.yml'
}

task pullData(type:JavaExec, dependsOn:classes) {
    main = 'com.tramchester.dataimport.FetchDataFromUrl'
    classpath = sourceSets.main.runtimeClasspath
    args 'http://odata.tfgm.com/opendata/downloads/TfGMgtfs.zip', 'data', 'tramData-1.0.zip'
}

task allowhost(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment) {
            WhiteList(port: '443', tag: 'web')
        }
    }
}

task blockhost(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment) {
            BlackList(port: '443', tag: 'web')
        }
    }
}

task buses(type:JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args 'server', 'config/buses.yml'
}

task busTest(type: Test) {
    useJUnit {
        includeCategories 'com.tramchester.BusTest'
    }
}

task unit(type: Test, dependsOn: compileJava) {
    useJUnit {
        filter {
            includeTestsMatching "com.tramchester.unit.*"
        }
        testLogging {
            exceptionFormat "full" // default is "short"
            events "passed", "skipped", "failed", "started",  "standardError"
        }
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }
}

task ci_unit(type: Test) {
    dependsOn 'allowhost'
    dependsOn 'compileJava'
    useJUnit {
        filter {
            includeTestsMatching "com.tramchester.unit.*"
        }
        testLogging {
            exceptionFormat "full" // default is "short"
            events "passed", "skipped", "failed", "started",  "standardError"
        }
    }
    finalizedBy {
        blockhost
    }
}

task integration(type: Test, dependsOn: compileJava) {
    maxHeapSize = '3500m'
    useJUnit {
        filter {
            includeTestsMatching "com.tramchester.integration.*"
        }
        testLogging {
            exceptionFormat "full" // default is "short"
            events "passed", "skipped", "failed", "started", "standardError", "standardOut"
        }
    }
}

task appium(type: Test) {
    dependsOn 'allowhost'
    dependsOn 'compileJava'
    useJUnit {
        filter {
            includeTestsMatching "com.tramchester.acceptance.*"
        }
        testLogging {
            exceptionFormat "full" // default is "short"
            events "passed", "skipped", "failed", "started", "standardError", "standardOut"
        }
    }
    finalizedBy {
        blockhost
    }
}

task localAppium(type: Test, dependsOn: compileJava) {
    useJUnit {
        filter {
            includeTestsMatching "com.tramchester.acceptance.*"
        }
        testLogging {
            events "passed", "skipped", "failed", "started", "standardError", "standardOut"
        }
    }
}

task uploadData(dependsOn: ['cfnassist', 'pullData']) {
    doLast {
        ant.cfnassist(buildNumber: buildNumber, bucketname: 'tramchester2dist') {
            Artifact(name: 'data', value: 'data/tramData-1.0.zip')
            S3Create()
        }
    }
}

task diagrams(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist() {
            Diagrams(target: '.')
        }
    }
}

distZip {
    into('config') {
        from 'config'
    }
}

task uploadApp(dependsOn: ['cfnassist', 'distZip', 'uploadSupportFiles'])  {
    doLast {
        ant.cfnassist(buildNumber: buildNumber, bucketname: 'tramchester2dist') {
            Artifact(name: 'package', value: 'build/distributions/tramchester-1.0.zip')
            S3Create()
        }
    }
}

task uploadSupportFiles(dependsOn: 'cfnassist') {
    doLast {
        ant.cfnassist(buildNumber: buildNumber, bucketname: 'tramchester2dist') {
            Artifact(name: 'install', value: 'deploy/setupTramWebServer.sh')
            Artifact(name: 'cloudinit', value: 'deploy/cloudInit.txt')
            S3Create()
        }
    }
}

task infra(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, sns: 'true') {
            Templates(target: 'deploy/infra')
        }
    }
}

task upload(dependsOn: ['uploadData', 'uploadApp'])

task deploy(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, buildNumber: buildNumber, sns: 'true') {
            Templates(target: 'deploy/servers.json')
        }
    }
}

task updateLB(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment, buildNumber: buildNumber, sns: 'true') {
            ELBUpdate(typeTag: 'web')
        }
    }
}

task tidy(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: environment) {
            TidyStacks(target: 'deploy/servers.json', typeTag: 'web')
        }
    }
}

task allowHostDev(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'Dev') {
            WhiteList(port: '443', tag: 'web')
        }
    }
}

task blockHostDev(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'Dev') {
            BlackList(port: '443', tag: 'web')
        }
    }
}

task whitelistUAT(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'UAT') {
            BlackList(port: '443', tag: 'web')
        }
    }
}

task blacklistUAT(dependsOn: cfnassist) {
    doLast {
        ant.cfnassist(project: 'tramchesterB', env: 'UAT') {
            BlackList(port: '443', tag: 'web')
        }
    }
}

task release(dependsOn: cfnassist) {
    doLast {
        def releaseNumber = System.getenv("RELEASE_NUMBER")
        if (releaseNumber) {
            logger.warn('Switch ELB to release '+releaseNumber)
            ant.cfnassist(project: 'tramchesterB', env: 'ProdGreen', buildNumber: releaseNumber, sns: 'true') {
                ELBUpdate(typeTag: 'web')
            }
        } else {
            logger.error('Please set env variable RELEASE_NUMBER')
        }

    }

}



